buildscript {
    repositories {
		mavenLocal()
		mavenCentral()
	}
}

plugins {
	// use java application packaging feature
	id 'application'
}

mainClassName = 'de.fraunhofer.iosb.testrunner.JMSTestRunner'

task dockerBuildImage (type: Exec) {
	group = 'Docker'
	description = 'command line task to create docker image'
	executable 'docker'
	args 'build', '-t', "ivct/ts-hla-encoding-rules:${TS_HLA_BaseVersion}", '--build-arg', "version=${TS_HLA_BaseVersion}", '.'
	dependsOn('distTar')
}

dependencies {
	compileOnly libraries.ieee
	implementation libraries.tc_lib
	compileOnly libraries.slf4j_api
	compileOnly libraries.logback_classic
	compileOnly libraries.logback_core
	compileOnly libraries.slf4j_jcl_over_slf4j
	compileOnly libraries.slf4j_jul_to_slf4j
	compileOnly libraries.slf4j_log4j_over_slf4j
	compileOnly libraries.jsonSimple

	testImplementation libraries.ieee
	testImplementation libraries.tc_lib
	testImplementation libraries.slf4j_api
	testImplementation libraries.logback_classic
	testImplementation libraries.logback_core
	testImplementation libraries.slf4j_jcl_over_slf4j
	testImplementation libraries.slf4j_jul_to_slf4j
	testImplementation libraries.slf4j_log4j_over_slf4j
  testImplementation(
          'junit:junit:4.12'
  )
  testImplementation(
          'org.junit.jupiter:junit-jupiter-api:5.1.0'
  )
  testRuntimeOnly(
          'org.junit.jupiter:junit-jupiter-engine:5.1.0',
          'org.junit.vintage:junit-vintage-engine:5.1.0'
  )
}

test {
  useJUnitPlatform()
}


// Write the  IVCT-Version as set to variable in libraries.gradle
// in a  property-file
task writeVersion {
    group = "Build"
    description = "Creates a properties file with version information"

    doLast {
        def prop = new Properties()
        prop.version = ivctVersion
        prop.ivctVersion = ivctVersion
        prop.TS_HLA_BaseVersion = TS_HLA_BaseVersion
        prop.build = "git describe --tags".execute().text.trim()
        mkdir "src/main/resources"
        File propFile = file('src/main/resources/testCaseBuild.properties')
        prop.store(propFile.newWriter(), null)
    }
}
processResources.dependsOn writeVersion

clean.doLast {
  file('src/main/resources/testCaseBuild.properties').delete()
}

// write IVCT-Version informations to manifest - file
jar{ 
  manifest{
     attributes(
     'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss").format(new Date()),
        'ivctVersion' : ivctVersion,
        'build' :       "git describe --tags".execute().text.trim()
     )
  }
}
